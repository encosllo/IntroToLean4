import a05Functions
import a10Equivalence
import a13EmptyUnit
import Paperproof
open Inj
open Surj
open Bij
open Empty
open Unit

namespace Product
-- ## Product type
-- The product type is a type that has two values
-- It is denoted by `A Ã— B` in Lean `times`
#check Prod
#print Prod

variable (A B : Type)
#check A Ã— B

-- To produce a value of type `A Ã— B`, we need to provide
-- a value of type `A` and a value of type `B`
def toPair {A B : Type} : A â†’ B â†’ A Ã— B := by
  intro a b
  exact âŸ¨a, bâŸ©

-- We can also use the `Prod.mk` constructor to produce a value of type `A Ã— B`
def toPair' {A B : Type} : A â†’ B â†’ A Ã— B := by
  intro a b
  exact Prod.mk a b

-- The `Prod` type has two projections, `fst` and `snd`,
-- that allow us to extract the first and second values, respectively
def Ï€1 {A B : Type} : (A Ã— B) â†’ A := by
  intro p
  exact p.fst

def Ï€2 {A B : Type} : (A Ã— B) â†’ B := by
  intro p
  exact p.snd

-- Two values of type `A Ã— B` are equal if and only if
-- their first and second components are equal
-- For this we can use the `Prod.ext` function
theorem prodEq {A B : Type} (p1 p2 : A Ã— B) : (p1 = p2) â†” (Ï€1 p1 = Ï€1 p2) âˆ§ (Ï€2 p1 = Ï€2 p2) := by
  apply Iff.intro
  -- p1 = p2 â†’ Ï€1 p1 = Ï€1 p2 âˆ§ Ï€2 p1 = Ï€2 p2
  intro h
  apply And.intro
  exact congrArg Ï€1 h
  exact congrArg Ï€2 h
  -- Ï€1 p1 = Ï€1 p2 âˆ§ Ï€2 p1 = Ï€2 p2 â†’ p1 = p2
  intro âŸ¨h1, h2âŸ©
  apply Prod.ext
  exact h1
  exact h2

-- The universal property of the product type is that
-- if we have a function `f : C â†’ A` and a function `g : C â†’ B`,
-- we can construct a function `h : C â†’ A Ã— B` such that
-- `Ï€1 âˆ˜ h = f` and `Ï€2 âˆ˜ h = g`
def toProd {A B C : Type} (f : C â†’ A) (g : C â†’ B) : (C â†’ A Ã— B) := by
  intro c
  exact Prod.mk (f c) (g c)

-- The `toProd` function satisfies that the corresponding
-- composition with the projections `Ï€1` and `Ï€2`
-- give us back the original functions `f` and `g`
theorem toProdp1 {A B C : Type} (f : C â†’ A) (g : C â†’ B) : Ï€1 âˆ˜ (toProd f g) = f := by
  funext c
  exact rfl

theorem toProdp2 {A B C : Type} (f : C â†’ A) (g : C â†’ B) : Ï€2 âˆ˜ (toProd f g) = g := by
  funext c
  exact rfl

-- The universal property also states that if we have
-- a function `h : C â†’ A Ã— B`, satisfying `Ï€1 âˆ˜ h = f`
-- and `Ï€2 âˆ˜ h = g`, then `h` must be equal to `toProd f g`
theorem toProdUnique {A B C : Type} {f : C â†’ A} {g : C â†’ B} {h : C â†’ A Ã— B} : (Ï€1 âˆ˜ h = f) â†’ (Ï€2 âˆ˜ h = g) â†’ (h = toProd f g) := by
  intro h1 h2
  funext c
  apply Prod.ext
  exact congrFun h1 c
  exact congrFun h2 c

-- ## Product type of a family of types
-- For a type of indices `I`, the product type of
-- a family of types `ð”¸ : I â†’ Type` is a type
-- that has a value for each type in the family
-- It is denoted by `âˆ€ (i : I), ð”¸ i` in Lean
variable (I : Type)
variable (ð”¸ : I â†’ Type)
#check âˆ€ (i : I), ð”¸ i

-- To produce a value of type `âˆ€ (i : I), ð”¸ i`, we need to
-- provide a value of type `ð”¸ i` for every `i : I`
def toPairg {I : Type} {ð”¸ : I â†’ Type} : ((i : I) â†’ ð”¸ i) â†’ âˆ€ (i : I), ð”¸ i := by
  intro ð•— i
  exact ð•— i

-- The `âˆ€ (i : I), ð”¸ i` type has a projection
-- that allows us to extract the value for a given `i : I`
def Ï€ {I : Type} {ð”¸ : I â†’ Type} (i : I) : (âˆ€ (i : I), ð”¸ i) â†’ ð”¸ i := by
  intro ð•’
  exact ð•’ i

-- Two values of type `âˆ€ (i : I), ð”¸ i` are equal if and only if
-- their `i`-th components are equal for every `i : I`
theorem prodEqg {I : Type} {ð”¸ : I â†’ Type} (ð•’1 ð•’2 : âˆ€ (i : I), ð”¸ i) : (ð•’1 = ð•’2) â†” âˆ€ (i : I), Ï€ i ð•’1 = Ï€ i ð•’2 := by
  apply Iff.intro
  -- ð•’1 = ð•’2 â†’ âˆ€ (i : I), Ï€ i ð•’1 = Ï€ i ð•’2
  intro h i
  exact congrArg (Ï€ i) h
  -- (âˆ€ (i : I), Ï€ i ð•’1 = Ï€ i ð•’2) â†’ ð•’1 = ð•’2
  intro h
  funext i
  exact h i

-- The universal property of the product type is that
-- if we have a function `ð•— i : C â†’ ð”¸ i` for every `i : I`
-- we can construct a function `h : C â†’ âˆ€ (i : I), ð”¸ i` such that
-- `(Ï€ i) âˆ˜ h = ð•— i` for every `i : I`
def toProdg {I C : Type} {ð”¸ : I â†’ Type} (ð•— : (i : I) â†’ C â†’ ð”¸ i) : C â†’ (âˆ€ (i : I), ð”¸ i) := by
  intro c i
  exact (ð•— i) c

-- The `toProdg` function satisfies that the corresponding
-- composition with the projections `Ï€ i`
-- give us back the original functions `ð•— i`
theorem toProdpg {I C : Type} {ð”¸ : I â†’ Type} (ð•— : (i : I) â†’ C â†’ ð”¸ i) (i : I) : (Ï€ i) âˆ˜ (toProdg ð•—) = ð•— i := by
  funext c
  exact rfl

-- The universal property also states that if if we have
-- a function `h : C â†’ âˆ€ (i : I), ð”¸ i`, satisfying `(Ï€ i) âˆ˜ h = (ð•— i)`
-- for every `i : I` then `h` must be equal to `toProdg f`
theorem toProdgUnique {I C : Type} {ð”¸ : I â†’ Type} {ð•— : (i : I) â†’ C â†’ ð”¸ i} {h : C â†’ âˆ€ (i : I), ð”¸ i} : (âˆ€ (i : I), (Ï€ i) âˆ˜ h = (ð•— i)) â†’ (h = toProdg ð•—) := by
  intro hp
  funext c
  funext i
  exact congrFun (hp i) c

-- ## Exercises
-- The product is commutative
theorem prodComm {A B : Type} : (A Ã— B) â‰… (B Ã— A) := by
  let f : (A Ã— B) â†’ (B Ã— A) := toProd Ï€2 Ï€1
  let g : (B Ã— A) â†’ (A Ã— B) := toProd Ï€2 Ï€1
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext e
    exact Prod.ext rfl rfl
    -- f âˆ˜ g = id
    funext e
    exact Prod.ext rfl rfl
  apply Nonempty.intro
  exact Subtype.mk f h

-- The product is associative
theorem prodAssoc {A B C : Type} : ((A Ã— B) Ã— C) â‰… (A Ã— (B Ã— C)) := by
  let f : ((A Ã— B) Ã— C) â†’ (A Ã— (B Ã— C)) := toProd (Ï€1 âˆ˜ Ï€1) (toProd (Ï€2 âˆ˜ Ï€1) Ï€2)
  let g : (A Ã— (B Ã— C)) â†’ ((A Ã— B) Ã— C) := toProd (toProd Ï€1 (Ï€1 âˆ˜ Ï€2)) (Ï€2 âˆ˜ Ï€2)
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext e
    exact Prod.ext rfl rfl
    -- f âˆ˜ g = id
    funext e
    exact Prod.ext rfl rfl
  apply Nonempty.intro
  exact Subtype.mk f h

-- Empty is a left zero
theorem TEmptyProdL {A : Type} : (Empty Ã— A) â‰… Empty := by
  let f : (Empty Ã— A) â†’ Empty := Ï€1
  let g : Empty â†’ (Empty Ã— A) := toProd id emptyToAny
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext e
    exact Empty.elim e.fst
    -- f âˆ˜ g = id
    exact toProdp1 id emptyToAny
  apply Nonempty.intro
  exact Subtype.mk f h

-- Empty is a right zero
theorem TEmptyProdR {A : Type} : (A Ã— Empty) â‰… Empty := by
  let f : (A Ã— Empty) â†’ Empty := Ï€2
  let g : Empty â†’ (A Ã— Empty) := toProd emptyToAny id
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext e
    exact Empty.elim e.snd
    -- f âˆ˜ g = id
    exact toProdp2 emptyToAny id
  apply Nonempty.intro
  exact Subtype.mk f h

-- Unit is a right unit
theorem TUnitProdR {A : Type} : (A Ã— Unit) â‰… A := by
  let f : (A Ã— Unit) â†’ A := Ï€1
  let g : A â†’ (A Ã— Unit) := toProd id anyToUnit
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext e
    exact Prod.ext rfl rfl
    -- f âˆ˜ g = id
    exact toProdp1 id anyToUnit
  apply Nonempty.intro
  exact Subtype.mk f h

-- Unit is a left unit
theorem TUnitProdL {A : Type} : (Unit Ã— A) â‰… A := by
  let f : (Unit Ã— A) â†’ A := Ï€2
  let g : A â†’ (Unit Ã— A) := toProd anyToUnit id
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext e
    exact Prod.ext rfl rfl
    -- f âˆ˜ g = id
    exact toProdp2 anyToUnit id
  apply Nonempty.intro
  exact Subtype.mk f h

end Product

namespace Sum
open Product
-- ## Sum type
-- The sum type is a type that has two values
-- It is denoted by `A âŠ• B` in Lean `oplus`
#check Sum
#print Sum

variable (A B : Type)
#check A âŠ• B

-- The `Sum` type has two injections, `Î¹1` and `Î¹2`,
-- that allow us to provide an element of type `A âŠ• B`
-- (using `Sum.inl` and `Sum.inr`, respectively)
def Î¹1 {A B : Type} : A â†’ A âŠ• B := by
  intro a
  exact Sum.inl a

def Î¹2 {A B : Type} : B â†’ A âŠ• B := by
  intro b
  exact Sum.inr b

-- Two values of type `A âŠ• B` are equal if and only if
-- they are both injections on the same element
theorem sumEq {A B : Type} (p1 p2 : A âŠ• B) : (p1 = p2) â†” (âˆƒ (a : A), (((Î¹1 a) = p1) âˆ§ ((Î¹1 a) = p2))) âˆ¨ (âˆƒ (b : B), (((Î¹2 b) = p1) âˆ§ ((Î¹2 b) = p2))) := by
  apply Iff.intro
  -- p1 = p2 â†’ (âˆƒ a, Î¹1 a = p1 âˆ§ Î¹1 a = p2) âˆ¨ âˆƒ b, Î¹2 b = p1 âˆ§ Î¹2 b = p2
  intro h
  cases p1 with
  | inl a => cases p2 with
    | inl b =>
      injection h with h1
      apply Or.inl
      apply Exists.intro a
      apply And.intro rfl
      rw [h1]
      exact rfl
    | inr b =>
      exact Sum.noConfusion h
  | inr a => cases p2 with
    | inl b =>
      exact Sum.noConfusion h
    | inr b =>
      injection h with h1
      apply Or.inr
      apply Exists.intro a
      apply And.intro rfl
      rw [h1]
      exact rfl
  -- ((âˆƒ a, Î¹1 a = p1 âˆ§ Î¹1 a = p2) âˆ¨ âˆƒ b, Î¹2 b = p1 âˆ§ Î¹2 b = p2) â†’ p1 = p2
  intro h
  cases h with
  | inl h =>
    apply Exists.elim h
    intro a âŸ¨h1, h2âŸ©
    exact h1.symm.trans h2
  | inr h =>
    apply Exists.elim h
    intro b âŸ¨h1, h2âŸ©
    exact h1.symm.trans h2

-- The universal property of the sum type is that
-- if we have a function `f : A â†’ C` and a function `g : B â†’ C`,
-- we can construct a function `h : A âŠ• B â†’ C` such that
-- `h âˆ˜ Î¹1 = f` and `h âˆ˜ Î¹2 = g`
def fromSum {A B C : Type} (f : A â†’ C) (g : B â†’ C) : (A âŠ• B) â†’ C := by
  intro o
  cases o with
  | inl a => exact f a
  | inr b => exact g b

-- The `fromSum` function satisfies that the corresponding
-- composition with the injections `Î¹1` and `Î¹2`
-- give us back the original functions `f` and `g`
theorem fromSumi1 {A B C : Type} (f : A â†’ C) (g : B â†’ C) : (fromSum f g) âˆ˜ Î¹1 = f := by
  funext a
  exact rfl

theorem fromSumi2 {A B C : Type} (f : A â†’ C) (g : B â†’ C) : (fromSum f g) âˆ˜ Î¹2 = g := by
  funext b
  exact rfl

-- The universal property also states that if we have
-- a function `h : A âŠ• B â†’ C`, satisfying `h âˆ˜ Î¹1 = f`
-- and `h âˆ˜ Î¹2 = g`, then `h` must be equal to `fromSum f g`
theorem fromSumUnique {A B C : Type} {f : A â†’ C} {g : B â†’ C} {h : (A âŠ• B) â†’ C} : (h âˆ˜ Î¹1 = f) â†’ (h âˆ˜ Î¹2 = g) â†’ (h = fromSum f g) := by
  intro h1 h2
  funext o
  cases o with
  | inl a => exact congrFun h1 a
  | inr b => exact congrFun h2 b

-- ## Sum type of a family of types
-- The sum type of a family of types is a type
-- that has a value for some type in the family
-- It is denoted by `(Î£ (i : I), ð”¸ i)` in Lean
variable (I : Type)
variable (ð”¸ : I â†’ Type)
#check Î£ i, ð”¸ i
#check (Î£ (i : I), ð”¸ i)

#check Sigma ð”¸
#print Sigma

-- The `(Î£ (i : I), ð”¸ i)` type has an injection
-- that allows us to insert the value for a given `i : I`
def Î¹ {I : Type} {ð”¸ : I â†’ Type} (i : I) : ð”¸ i â†’ (Î£ (i : I), ð”¸ i) := by
  intro a
  exact âŸ¨i, aâŸ©

-- Two values of type `(Î£ (i : I), ð”¸ i)` are equal if and
-- only if they are both `Î¹ i` of some `a : ð”¸ i`
-- For this we will use `Sigma.ext`
theorem sumEqg {I : Type} {ð”¸ : I â†’ Type} (ð•’1 ð•’2 : (Î£ (i : I), ð”¸ i)) : (ð•’1 = ð•’2) â†” âˆƒ (i : I), âˆƒ (a : ð”¸ i), (ð•’1 = Î¹ i a) âˆ§ (ð•’2 = Î¹ i a) := by
  apply Iff.intro
  -- ð•’1 = ð•’2 â†’ âˆƒ i a, ð•’1 = Î¹ i a âˆ§ ð•’2 = Î¹ i a
  intro h
  cases ð•’1 with
  | mk i a => cases ð•’2 with
    | mk j b =>
      injection h with h1 h2
      apply Exists.intro i
      apply Exists.intro a
      apply And.intro rfl
      exact Sigma.ext h1.symm h2.symm
  -- âˆƒ i a, ð•’1 = Î¹ i a âˆ§ ð•’2 = Î¹ i a â†’ ð•’1 = ð•’2
  intro âŸ¨i, âŸ¨a, âŸ¨h1, h2âŸ©âŸ©âŸ©
  exact  h1.trans h2.symm

-- The universal property of the sum type is that
-- if we have a function `ð•— i : ð”¸ i â†’ C` for every `i : I`
-- we can construct a function `h : (Î£ (i : I), ð”¸ i) â†’ C` such that
-- `h âˆ˜ (Î¹ i) = ð•— i` for every `i : I`
def fromSumg {I C : Type} {ð”¸ : I â†’ Type} (ð•— : (i : I) â†’ ð”¸ i â†’ C) : (Î£ (i : I), ð”¸ i) â†’ C := by
  intro âŸ¨i, aâŸ©
  exact ð•— i a

-- The `fromSumg` function satisfies that the corresponding
-- composition with the injections `Î¹ i`
-- give us back the original functions `ð•— i`
theorem fromSumig {I C : Type} {ð”¸ : I â†’ Type} (ð•— : (i : I) â†’ ð”¸ i â†’ C) (i : I) : (fromSumg ð•—) âˆ˜ (Î¹ i) = ð•— i := by
  funext c
  exact rfl

-- The universal property also states that if we have
-- a function `h : (Î£ (i : I), ð”¸ i) â†’ C`, satisfying `h âˆ˜ (Î¹ i) = ð•— i`
-- for every `i : I` then `h` must be equal to `fromSumg ð•—`
theorem fromSumgUnique {I C : Type} {ð”¸ : I â†’ Type} {ð•— : (i : I) â†’ ð”¸ i â†’ C} {h : (Î£ (i : I), ð”¸ i) â†’ C} : (âˆ€ (i : I), h âˆ˜ (Î¹ i) = (ð•— i)) â†’ (h = fromSumg ð•—) := by
  intro hs
  funext âŸ¨i, aâŸ©
  exact congrFun (hs i) a

-- ## Exercises
-- The sum is commutative
theorem sumComm {A B : Type} : (A âŠ• B) â‰… (B âŠ• A) := by
  let f : (A âŠ• B) â†’ (B âŠ• A) := fromSum (Î¹2) (Î¹1)
  let g : (B âŠ• A) â†’ (A âŠ• B) := fromSum (Î¹2) (Î¹1)
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext e
    match e with
      | inl a => rfl
      | inr b => rfl
    -- f âˆ˜ g = id
    funext e
    match e with
      | inl a => rfl
      | inr b => rfl
  apply Nonempty.intro
  exact Subtype.mk f h

-- The sum is associative
theorem sumAssoc {A B C : Type} : ((A âŠ• B) âŠ• C) â‰… (A âŠ• (B âŠ• C)) := by
  let f : ((A âŠ• B) âŠ• C) â†’ (A âŠ• (B âŠ• C)) := fromSum (fromSum (Î¹1) (Î¹2 âˆ˜ Î¹1)) (Î¹2 âˆ˜ Î¹2)
  let g : (A âŠ• (B âŠ• C)) â†’ ((A âŠ• B) âŠ• C) := fromSum (Î¹1 âˆ˜ Î¹1) (fromSum (Î¹1 âˆ˜ Î¹2) (Î¹2))
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext e
    match e with
      | inl ab => match ab with
        | inl a => rfl
        | inr b => rfl
      | inr c => rfl
    -- f âˆ˜ g = id
    funext e
    match e with
      | inl a => rfl
      | inr bc => match bc with
        | inl b => rfl
        | inr c => rfl
  apply Nonempty.intro
  exact Subtype.mk f h

-- Empty is a left unit
theorem TEmptySumL {A : Type} : (Empty âŠ• A) â‰… A := by
  let f : (Empty âŠ• A) â†’ A := fromSum emptyToAny id
  let g : A â†’ (Empty âŠ• A) := Î¹2
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext e
    match e with
      | inl z => exact Empty.elim z
      | inr a => rfl
    -- f âˆ˜ g = id
    funext e
    exact rfl
  apply Nonempty.intro
  exact Subtype.mk f h

-- Empty is a right unit
theorem TEmptySumR {A : Type} : (A âŠ• Empty) â‰… A := by
  let f : (A âŠ• Empty) â†’ A := fromSum id emptyToAny
  let g : A â†’ (A âŠ• Empty) := Î¹1
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext e
    match e with
      | inl a => rfl
      | inr z => exact Empty.elim z
    -- f âˆ˜ g = id
    funext e
    exact rfl
  apply Nonempty.intro
  exact Subtype.mk f h

-- Product distributes over sum on the right
theorem TProdSumDistR {A B C : Type} : (A Ã— (B âŠ• C)) â‰… ((A Ã— B) âŠ• (A Ã— C)) := by
  let f : (A Ã— (B âŠ• C)) â†’ ((A Ã— B) âŠ• (A Ã— C)) := by
    intro âŸ¨a, bcâŸ©
    match bc with
      | inl b => exact Sum.inl (âŸ¨a, bâŸ©)
      | inr c => exact Sum.inr (âŸ¨a, câŸ©)
  let g : ((A Ã— B) âŠ• (A Ã— C)) â†’ (A Ã— (B âŠ• C)) := fromSum (toProd Ï€1 (Î¹1 âˆ˜ Ï€2)) (toProd Ï€1 (Î¹2 âˆ˜ Ï€2))
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext âŸ¨a, bcâŸ©
    match bc with
      | inl b => rfl
      | inr c => rfl
    -- f âˆ˜ g = id
    funext e
    match e with
      | inl âŸ¨a,bâŸ© => rfl
      | inr âŸ¨a,câŸ© => rfl
  apply Nonempty.intro
  exact Subtype.mk f h

-- Product distributes over sum on the left
theorem TProdSumDistL {A B C : Type} : ((A âŠ• B) Ã— C) â‰… ((A Ã— C) âŠ• (B Ã— C)) := by
  let f : ((A âŠ• B) Ã— C) â†’ ((A Ã— C) âŠ• (B Ã— C)) := by
    intro âŸ¨ab, câŸ©
    match ab with
      | inl a => exact Sum.inl (âŸ¨a, câŸ©)
      | inr b => exact Sum.inr (âŸ¨b, câŸ©)
  let g : ((A Ã— C) âŠ• (B Ã— C)) â†’ ((A âŠ• B) Ã— C) := fromSum (toProd (Î¹1 âˆ˜ Ï€1) Ï€2) (toProd (Î¹2 âˆ˜ Ï€1) Ï€2)
  let h : isomorphism f := by
    apply Exists.intro g
    apply And.intro
    -- g âˆ˜ f = id
    funext âŸ¨ab, câŸ©
    match ab with
      | inl a => rfl
      | inr b => rfl
    -- f âˆ˜ g = id
    funext e
    match e with
      | inl âŸ¨a,câŸ© => rfl
      | inr âŸ¨b,câŸ© => rfl
  apply Nonempty.intro
  exact Subtype.mk f h

end Sum
